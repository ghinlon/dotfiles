# Copyright 2011 Google Inc. All Rights Reserved.
# This file is available under the Apache license.

# A mtail module for monitoring vsftpd logs
#
# Configure your vsftpd to write the xferlog as well as vsftpd.log

hidden text direction
hidden text username
hidden text remotehost

counter vsftpd_transferred_bytes_total by direction, user, remotehost
counter vsftpd_transfer_seconds_total by direction, user, remotehost
counter vsftpd_transfer_completed_total by direction, user, remotehost

def vsftpd_timestamp {
  # Mon Feb 21 15:21:32 2011
  /^\w+\s(?P<date>\w+\s+\d+\s\d+:\d+:\d+\s\d+)/ {
    strptime($date, "Jan _2 15:04:05 2006")
    next
  }
}

const XFERLOG_RE // +
    # e.g. 1 172.18.115.36 528
    # time spent transferring
    /\s(?P<transfertime>\d+)/ +
    # remote host
    /\s(?P<remotehost>\d+\.\d+\.\d+\.\d+)/ +
    # bytes transferred
    /\s(?P<bytestransferred>\d+)/ +
    # filename
    /\s(?P<filename>\S+)/ +
    # e.g. b _ i a anonymous@ ftp 0 * c
    # transfertype
    /\s\S/ +
    # special action flag
    /\s\S/ +
    # direction
    /\s(?P<direction>\S)/ +
    # access mode
    /\s\S/ +
    # username
    /\s(?P<username>\S+)/ +
    # service name
    /\s\S+/ +
    # authentication method
    /\s\d/ +
    # authenticated id
    /\s\S+/ +
    # completion status
    /\s(?P<completionstatus>\S)/

@vsftpd_timestamp {
  getfilename() =~ /xferlog/ {
    // + XFERLOG_RE {
      # Handles log entries from the wuftpd format xferlog.
      $direction == "i" {
        direction = "incoming"
      }
      $direction == "o" {
        direction = "outgoing"
      }
	  username = $username
	  remotehost = $remotehost
      $completionstatus == "c" {
        vsftpd_transfer_completed_total[direction,username,remotehost]++
      }
      vsftpd_transfer_seconds_total[direction,username,remotehost] += $transfertime
      vsftpd_transferred_bytes_total[direction,username,remotehost] += $bytestransferred
    }
  }
 } 
